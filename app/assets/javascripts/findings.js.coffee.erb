jQuery ($) ->
  $(document).on 'change', '[data-repeated-url]', ->
    repeatedSelect = $(this)
    repeatedId     = repeatedSelect.val()
    urlTemplate    = decodeURI repeatedSelect.data('repeatedUrl')
    url            = urlTemplate.replace '[FINDING_ID]', repeatedId
    fields         = [
      'title', 'description', 'effect', 'audit_recommendations', 'risk',
      'priority', 'answer', 'audit_comments'
    ]

    if repeatedId
      repeatedSelect.prop 'disabled', true

      $.getJSON url, (finding) ->
        $.each fields, (i, field) ->
          $("[name$=\"[#{field}]\"]").val finding[field]

        $.each ['follow_up_date', 'origination_date'], (i, dateField) ->
          date = new Date finding[dateField]

          date.setMinutes new Date().getTimezoneOffset()
          $("[name$='[#{dateField}]']").datepicker()
          $("[name$='[#{dateField}]']").datepicker('setDate', date)
      .always -> repeatedSelect.prop 'disabled', false
    else
      $.each fields, (i, field) -> $("[name$=\"[#{field}]\"]").val ''
